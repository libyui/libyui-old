// Create a combo box with three entries.
// All entries have IDs to identify them independent of locale
// (The texts might have to be translated!).
// Entry "Funghi" will be selected by default.
{
    UI::OpenDialog(
	       `VBox(
		     `ComboBox(`id(`pizza), `opt(`editable),
				    "Select your Pizza:",
				    [
				     `item(`id(`nap), "Napoli"       ),
				     `item(`id(`fun), "Funghi", true ),
				     `item(`id(`sal), "Salami"       )
				    ] ),
		     `HBox(
			   `PushButton(`id(`empty), "&Empty"),
			   `PushButton(`id(42), "42"),
			   `PushButton(`id(`ok),"&OK")
			   )
		     )
	       );

    any button = nil;
    
    repeat
    {
	y2debug( "Waiting" );
	button = UI::UserInput();
	y2debug( "button: %1", button );

	if ( button == 42 )
	    UI::ChangeWidget(`pizza, `Value, 42 );

	if ( button == `empty )
	    UI::ChangeWidget(`pizza, `Value, "" );
    } until ( button == `ok );
    

    // Get the input from the selection box.
    //
    // Notice: The return value of UI::UserInput() does NOT return this value!
    // Rather, it returns the ID of the widget (normally the PushButton)
    // that caused UI::UserInput() to return.
    any pizza=UI::QueryWidget(`id(`pizza), `Value);
    y2milestone( "Selected %1", pizza );

    UI::CloseDialog();
}
