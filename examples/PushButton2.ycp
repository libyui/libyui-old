{
    // Build dialog with three buttons.
    // "Cancel" is the default button, i.e. pressing "Return" will
    // activate it.
    
    UI::OpenDialog(
		   `HBox(
			 `PushButton( `id(`ok),			    "&OK"     ),
			 `PushButton( `id(`cancel), `opt(`default), "&Cancel" ),
			 `PushButton( `id(`help),	   	    "&Help"   )
			 )
		   );

    // Wait for user input. The value returned is the ID of the widget
    // that makes UI::UserInput() terminate, i.e. the respective button ID.
    any button_id = UI::UserInput();
    
    // Close the dialog.
    UI::CloseDialog();


    // Process the input.
    string button_name = "";
    if 		( button_id == `ok 	) 	button_name = "OK";
    else if	( button_id == `cancel	) 	button_name = "Cancel";
    else if	( button_id == `help 	) 	button_name = "Help";

    // Pop up a new dialog to display what button was clicked.
    UI::OpenDialog(
		   `VBox(
			 `Label( "You clicked button \"" + button_name + "\"."),
			 `PushButton( `opt(`default), "&OK" )
			 )
		   );
    UI::UserInput();
    UI::CloseDialog();
}

