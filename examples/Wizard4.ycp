// Advanced example of using the Wizard widget.
//
// Note: YCP applications are discouraged from using the Wizard widget directly.
// Use the Wizard module instead.

{
    if ( ! UI::HasSpecialWidget(`Wizard) )
    {
	y2error( "This works only with UIs that provide the wizard widget!" );
	return;
    }

    string help_text =
	"<p>This is a help text.</p>"
	+ "<p>It should be helpful.</p>"
	+ "<p>If it isn't helpful, it should rather not be called a <i>help text</i>.</p>";

    UI::OpenDialog(`opt(`defaultsize ),
		   `Wizard(`opt(`treeEnabled),
			   `back,  "&Back",
			   `abort, "Ab&ort",
			   `next,  "&Next" ) );
    
    // UI::DumpWidgetTree();

    
    UI::WizardCommand(`SetDialogIcon( "/usr/share/YaST2/theme/SuSELinux/icons/22x22/apps/user_add.png" ) );
    UI::WizardCommand(`SetDialogHeading( "Welcome to the YaST2 installation" ) );
    UI::WizardCommand(`SetHelpText( help_text ) );


    UI::WizardCommand(`AddTreeItem( "", "First Toplevel Item" , "tl1" ) );
    UI::WizardCommand(`AddTreeItem( "", "Second Toplevel Item", "tl2" ) );
    UI::WizardCommand(`AddTreeItem( "", "Third Toplevel Item" , "tl3" ) );
		      
    UI::WizardCommand(`AddTreeItem( "tl1", "First Sublevel"  , "1-1" ) );
    UI::WizardCommand(`AddTreeItem( "tl1", "Second Sublevel" , "1-2" ) );
    UI::WizardCommand(`AddTreeItem( "tl1", "Third Sublevel"  , "1-3" ) );

    UI::WizardCommand(`AddTreeItem( "tl2", "First Sublevel"  , "2-1" ) );
    UI::WizardCommand(`AddTreeItem( "tl2", "Second Sublevel" , "2-2" ) );
    UI::WizardCommand(`AddTreeItem( "tl2", "Third Sublevel"  , "2-3" ) );
    
    UI::WizardCommand(`AddTreeItem( "1-2", "First 3rd level "  , "3rd 1" ) );
    UI::WizardCommand(`AddTreeItem( "1-2", "Second 3rd level " , "3rd 2" ) );
    UI::WizardCommand(`AddTreeItem( "1-2", "Item without ID"   , "" ) );

    UI::WizardCommand(`SelectTreeItem( "3rd 1" ) );


    
    UI::WizardCommand(`AddMenu( "&File",	"file-menu" ) );
    UI::WizardCommand(`AddMenu( "&Edit",	"edit-menu" ) );
    UI::WizardCommand(`AddMenu( "&Options",	"opt-menu"  ) );
    
    UI::WizardCommand(`AddMenuEntry	( "file-menu", "&New", 		"file-new" 	) );
    UI::WizardCommand(`AddMenuEntry	( "file-menu", "&Open", 	"file-open" 	) );
    UI::WizardCommand(`AddSubMenu	( "file-menu", "Open &Recent", 	"file-recent"  	) );
    UI::WizardCommand(`AddMenuEntry	( "file-menu", "&Save", 	"file-save" 	) );
    UI::WizardCommand(`AddMenuEntry	( "file-menu", "Save &As", 	"file-save-as" 	) );
		      
    UI::WizardCommand(`AddMenuEntry	( "file-recent", "/tmp/test1", 	"recent-test1" 	) );
    UI::WizardCommand(`AddMenuEntry	( "file-recent", "/tmp/test2", 	"recent-test2" 	) );
		      
    UI::WizardCommand(`AddMenuEntry	( "edit-menu", "C&ut",		"edit-cut"	) );
    UI::WizardCommand(`AddMenuEntry	( "edit-menu", "C&opy",		"edit-copy"	) );
    UI::WizardCommand(`AddMenuEntry	( "edit-menu", "&Paste",	"edit-paste"	) );
		      
    UI::WizardCommand(`AddMenuEntry	( "opt-menu", 	"&Settings",	"opt-settings"	) );
    UI::WizardCommand(`AddMenuSeparator	( "opt-menu" ) );
    UI::WizardCommand(`AddMenuEntry	( "opt-menu", 	"Activate &Hypersonic Transducer", "frank-n-furter" ) );

    

    while ( true )
    {
	map event = UI::WaitForEvent();

	y2milestone( "Got event: %1", event );

	if ( event[ "ID" ]:nil == `abort )
	    break;

	y2milestone( "Tree selection: %1", UI::QueryWidget(`id(`wizard), `CurrentItem ) );
    }

    UI::CloseDialog();

}
