// -*- coding: utf-8 -*-
{
    // Example for UI::SetLanguage() UI::GetLanguage()
    
    UI::OpenDialog(
	       `VBox(
		     `InputField(`id(`lang_field), "&Language:"),
		     `HBox(
			   `Label("Current language:"),
			   `Label(`id(`lang_label), `opt(`outputField, `hstretch), UI::GetLanguage(false))
			   ),
		     `HBox(
			   `PushButton(`id(`set),		"UI::&SetLanguage()\n"),
			   `PushButton(`id(`get_strip),		"UI::&GetLanguage()\nstrip encoding"),
			   `PushButton(`id(`get_nostrip),	"&UI::GetLanguage()\n&with encoding"),
			   `PushButton(`id(`test_dialog),	"&Test Dialog" ),
			   `HSpacing(3),
			   `PushButton(`id(`close), "&Close" )
			   )
		     )
	       );

    UI::SetFocus(`id(`lang_field));
	     
    while(true)
    {
	any id = UI::UserInput();

	if ( id == `close )
	{
	    UI::CloseDialog();
	    return;
	}
	else if ( id == `get_strip )
	{
	    UI::ChangeWidget(`id(`lang_field), `Value, UI::GetLanguage(true));
	}
	else if ( id == `get_nostrip )
	{
	    UI::ChangeWidget(`id(`lang_field), `Value, UI::GetLanguage(false));
	}
	else if ( id == `set )
	{
	    UI::SetLanguage((string) UI::QueryWidget(`id(`lang_field), `Value));
	    UI::ChangeWidget(`id(`lang_label), `Value, UI::GetLanguage(false));
	}
	else if ( id == `test_dialog )
	{
	    UI::OpenDialog(
			   `VBox(
				 `Heading( "Test Dialog" ),
				 `Label( "Fonts may have changed\nGrüß Gott\nCzech (česky)\nGreek (Ελληνικά)\nRussian (Русский)\n漢字かな直\nHangul (한글)" ),
				 `PushButton(`opt(`default), "&OK" )
				 )
			   );
	    UI::UserInput();
	    UI::CloseDialog();
	}
    }
}

